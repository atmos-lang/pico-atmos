;;LUA_PATH="/x/atmos/src/?.lua;" lua5.4 /x/atmos/src/atmos.lua click-drag-cancel.atm

val SDL = require "SDL"
val IMG = require "SDL.image"

defer {
    IMG.quit()
    SDL.quit()
}

val WIN = SDL.createWindow @{
	title  = "Birds - 01 (task)",
	width  = 640,
	height = 480,
    flags  = @{ SDL.flags.OpenGL },
} -> assert
val REN = SDL.createRenderer(WIN,-1) -> assert

val UP = ren where {
    sfc = IMG.load("bird-up.png") -> assert
    ren = REN::createTextureFromSurface(sfc) -> assert
}
val DN = ren where {
    sfc = IMG.load("bird-dn.png") -> assert
    ren = REN::createTextureFromSurface(sfc) -> assert
}
val _,_,W,H = UP::query()

func Bird (y, speed) {
    var xx = 0
    var yy = y
    var ang = 0
    par {
        every :Pico.Frame {
            val v = 50 * speed
            set xx = xx + (v/1000)
            set yy = y - ((speed/5) * math.sin(ang))
            set ang = ang + ((3.14*v)/100000)
        }
    } with {
        every :Pico.Draw {
            val img = match math.floor(((ang+(3.14/2))/3.14)) % 2 {
                0 => DN
                1 => UP
            }
            REN::copy(img)
        }
    }
}

spawn {
    every :Pico.Draw {
        REN::setDrawColor(0x000000)
        REN::clear()
    }
}
spawn Bird (150, 100)
spawn Bird (350, 200)
spawn {
    every :Pico.Draw {
        REN::present()
    }
}

loop {
	val e = SDL.waitEvent(50)
    if e != nil {
        match e.type {
            SDL.event.Quit            => break()
            SDL.event.MouseButtonUp   => emit(:Pico.Mouse.Button.Up(e))
            SDL.event.MouseButtonDown => emit(:Pico.Mouse.Button.Dn(e))
            SDL.event.MouseMotion     => emit(:Pico.Mouse.Motion(e))
            SDL.event.KeyDown => {
                set e.name = SDL.getKeyName(e.keysym.sym)
                emit(:Pico.Key.Dn(e))
            }
        }
    }
    emit(:Pico.Frame, 50)
    emit(:Pico.Draw)
}
